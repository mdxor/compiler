blank_line = { (" " | "\t")* ~ ("\r\n" | "\n" | EOI) }

line_end = { "\r\n" | "\n" | EOI }
atx_heading_marker = { "#"{1, 6} }
atx_heading_start = { atx_heading_marker ~ (" " | "\t" | line_end ) }

open_fenced_code_meta = { (!("`" | "\r" | "\n" | "\x00") ~ ANY)* }
open_fenced_code_backtick = _{ open_fenced_code_backtick_match ~ open_fenced_code_meta }
open_fenced_code_backtick_match = { "`"{3,} }
open_fenced_code_tilde = _{ open_fenced_code_tilde_match ~ open_fenced_code_meta }
open_fenced_code_tilde_match = { "~"{3,} }
open_fenced_code = { (open_fenced_code_backtick | open_fenced_code_tilde) ~ ("\r" | "\n" | EOI ) }

close_fenced_code = { close_fenced_code_match ~ ("\t" | " ")* ~ ("\r" | "\n" | EOI ) }
close_fenced_code_match = { "`"{3,} | "~"{3,} }


setext_heading = { ("="+ | "-"+) ~ (" " | "\t")* ~ ("\r" | "\n") }
thematic_break = { (("*" ~ (" " | "\t")*){3,} | ("_" ~ (" " | "\t")*){3,} | ("-" ~ (" " | "\t")*){3,}) ~ (" " | "\t")* ~ ("\r" | "\n") }

footnote_definition = { "[^" ~ (!("]" | "\r" | "\n" | "\x00" | "\t") ~ ANY)+ ~ "]:" ~ (" " | "\t")* }

scheme = _{ ('A'..'Z' | 'a'..'z') ~ ('A'..'Z' | 'a'..'z' | '0'..'9' | "." | "+" | "-"){1,31} }

scheme_rule = { scheme ~ ":" }

autolink_uri = { scheme ~ ":" ~ (!('\x00'..'\x20' | "<" | ">") ~ ANY)* ~ ">" }
autolink_email = { ('a'..'z' | 'A'..'Z' | '0'..'9' | "." | "!" | "#" | "$" | "%" | "&" | "'" | "*" | "+" | "/" | "=" | "?" | "^" | "_" | "`" | "{" | "|" | "}" | "~" | "-")+ ~ "@" ~ ('a'..'z' | 'A'..'Z' | '0'..'9') ~ (('a'..'z' | 'A'..'Z' | '0'..'9' | "-"){0,61} ~ ('a'..'z' | 'A'..'Z' | '0'..'9')?)? ~ ("." ~ (('a'..'z' | 'A'..'Z' | '0'..'9' | "-"){0,61} ~ ('a'..'z' | 'A'..'Z' | '0'..'9')?)?)* ~ ">" }

escaped_char = _{ "\\" ~ ANY }
link_title = { "\"" ~ (escaped_char | (!("\"" | "\x00") ~ ANY))* ~ "\"" | "'" ~ (escaped_char | (!("'" | "\x00") ~ ANY))* ~ "'" | "(" ~ (escaped_char | (!("(" | ")" | "\x00") ~ ANY))* ~ ")" }

table_spacechar = _{ " " | "\t" | "\x0b" | "\x0c" }
table_newline = _{ "\r"? ~ "\n" }
table_marker = _{ table_spacechar* ~ ":"? ~ "-"+ ~ ":"? ~ table_spacechar* }
table_cell = { ( escaped_char | !("|" | "\r" | "\n") ~ ANY)* }

table_start = { "|"? ~ table_marker ~ ("|" ~ table_marker)* ~ "|"? ~ table_spacechar* ~ table_newline }
table_cell_end = { "|" ~ table_spacechar* ~ table_newline? }
table_row_end = { table_spacechar* ~ table_newline }

digit_list_item_marker = _{ ( '0'..'9' ){1,10} ~ ( "." | ")" ) }
list_item_marker = { "-" | "+" | "*" | digit_list_item_marker }
list_item_start = { list_item_marker ~ (" " | line_end) }